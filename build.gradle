apply plugin: 'java-library'

group 'org.comroid'
version '0.3.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.findByName('wrapper')?.configure {
    gradleVersion = '6.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply from: "gradle/vars.gradle"

tasks.withType(Javadoc) {
    source = sourceSets.main.java

    options {
        encoding = 'UTF-8'
        /*
        links = [
                "https://docs.oracle.com/javase/8/docs/api/",
                "https://docs.oracle.com/javaee/7/api/"
        ]
        */
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'

    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

processResources {
    from(sourceSets.main.resources) {
        expand project.properties
    }
}

compileJava.options.encoding = 'UTF-8'

repositories {
    maven { url 'https://maven.comroid.org' }
    mavenLocal()
    jcenter()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:19.0.0'

    // logging dependencies
    implementation 'org.apache.logging.log4j:log4j-api:2.11.0'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.11.0'

    if (isAutoDeploy) {
        implementation 'org.comroid:restless:0.+'
    } else {
        api project(':guardian:restless') == null
                ? 'org.comroid:restless:0.16.0-SNAPSHOT'
                : project(':guardian:restless')

        testImplementation project(':guardian:uniform').sourceSets.java8compat.output.classesDirs
        testImplementation project(':guardian:restless').sourceSets.java11compat.output.classesDirs
    }
    testImplementation 'com.alibaba:fastjson:1.2.70'
}

apply from: "gradle/publishing.gradle"
